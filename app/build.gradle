plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.symja.app'
    compileSdk 36

    defaultConfig {
        applicationId "com.symja.app"
        minSdk 31
        versionCode 1
        versionName "3.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            
        }
    }

    compileOptions {
        // https://developer.android.com/studio/write/java8-support
        // For AGP 4.1+
        coreLibraryDesugaringEnabled = true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
            pickFirsts += ['**/*.xsd',
                           'META-INF/*.md',
                           '**/LICENSE.txt',
                           '**/Messages.properties',
                           '**/Log4j2Plugins.dat']
        }
    }

    testOptions {
        animationsDisabled = true
    }

    buildFeatures {
        buildConfig = true
    }
}


// https://github.com/Celeral/log4j2-android
configurations.configureEach { config ->
    {
        if (config.name.startsWith("test") || config.name.contains("Test") && !config.name.contains("Android")) {
            exclude group: "com.celeral", module: "log4j2-android"
        } else {
            exclude group: "org.apache.logging.log4j", module: "log4j-core"
        }
    }
    resolutionStrategy {
        force "org.apache.logging.log4j:log4j-api:2.17.2"
    }
}


dependencies {
    configurations.configureEach {
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
            select("com.google.guava:guava:0")
        }
        // and/or
        resolutionStrategy.capabilitiesResolution.withCapability("com.google.guava:listenablefuture") {
            select("com.google.guava:guava:0")
        }
    }

    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.1.5")

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.3'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation "androidx.browser:browser:1.8.0"

    implementation project(':modules:programming')

    // https://github.com/Celeral/log4j2-android
    runtimeOnly "com.celeral:log4j2-android:1.0.0"

}